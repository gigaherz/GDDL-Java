group 'dev.gigaherz.util.gddl'
version '2.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceCompatibility = targetCompatibility = 11
compileJava {
    sourceCompatibility = targetCompatibility = 11
}

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
}

sourceSets {
    dynamic {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    demo {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            compileClasspath += dynamic.output
            runtimeClasspath += dynamic.output
            compileClasspath += dynamic.compileClasspath
            runtimeClasspath += dynamic.runtimeClasspath
        }
    }
    test {
        java {
            compileClasspath += dynamic.output
            runtimeClasspath += dynamic.output
            compileClasspath += dynamic.compileClasspath
            runtimeClasspath += dynamic.runtimeClasspath
        }
    }
}

configurations {
    dynamicImplementation.extendsFrom implementation
    demoImplementation.extendsFrom implementation, dynamicImplementation
}

dependencies {
    compileOnly 'org.jetbrains:annotations:20.1.0'

    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile "com.google.guava:guava:29.0-jre"

    dynamicImplementation 'com.mojang:datafixerupper:4.0.26'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    baseName "{project.name}-dynamic"
    classifier 'sources'
}

task dynamicJar(type: Jar, dependsOn: classes) {
    from sourceSets.dynamic.output
    baseName "${project.name}-dynamic"
    classifier ''
}

task dynamicSourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.dynamic.allSource
    baseName "${project.name}-dynamic"
    classifier 'sources'
}

artifacts {
    archives sourcesJar, dynamicJar, dynamicSourcesJar
}

tasks.assemble.dependsOn sourcesJar, dynamicJar, dynamicSourcesJar

publishing {
    publications {
        main(MavenPublication) {
            artifactId 'gddl'

            artifact jar
            artifact sourcesJar
        }
        dynamic(MavenPublication) {
            artifactId 'gddl-dynamic'

            artifact dynamicJar
            artifact dynamicSourcesJar
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "GDDL",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "2.0", // We are version 1 of ourselves
                "Implementation-Title": "GDDL",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
