group 'dev.gigaherz.util.gddl'
version '2.0.0-alpha.4'

apply plugin: 'java'
apply plugin: 'maven-publish'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
}

sourceSets {
    dynamic {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    demo {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            compileClasspath += dynamic.output
            runtimeClasspath += dynamic.output
            compileClasspath += dynamic.compileClasspath
            runtimeClasspath += dynamic.runtimeClasspath
        }
    }
    test {
        java {
            compileClasspath += dynamic.output
            runtimeClasspath += dynamic.output
            compileClasspath += dynamic.compileClasspath
            runtimeClasspath += dynamic.runtimeClasspath
        }
    }
}

configurations {
    dynamicImplementation.extendsFrom implementation
    demoImplementation.extendsFrom implementation, dynamicImplementation
}

test {
    useJUnitPlatform()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testImplementation 'com.google.guava:guava:31.1-jre'

    dynamicImplementation 'com.mojang:datafixerupper:6.0.6'
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "GDDL",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "2.0",
                "Implementation-Title": "GDDL",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'

    from('/') {
        include 'LICENSE.txt'
    }
}

task dynamicJar(type: Jar, dependsOn: classes) {
    from sourceSets.dynamic.output
    baseName "${project.name}-dynamic"
    classifier ''

    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "GDDL Dynamic",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "2.0",
                "Implementation-Title": "GDDL Dynamic",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task dynamicSourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.dynamic.allSource
    baseName "${project.name}-dynamic"
    classifier 'sources'

    from('/') {
        include 'LICENSE.txt'
    }
}

artifacts {
    archives sourcesJar, dynamicJar, dynamicSourcesJar
}

tasks.assemble.dependsOn sourcesJar, dynamicJar, dynamicSourcesJar

publishing {
    publications {
        main(MavenPublication) {
            artifactId 'gddl2'

            artifact jar
            artifact sourcesJar
        }
        dynamic(MavenPublication) {
            artifactId 'gddl2-dynamic'

            artifact dynamicJar
            artifact dynamicSourcesJar
        }
    }

    repositories {
        if (findProperty("RELEASE") && System.env.giga_maven_host != null) {
            System.out.println("Remote publish enabled on " + System.env.giga_maven_host)
            maven {
                url System.env.giga_maven_host
                credentials {
                    username System.env.giga_maven_user
                    password System.env.giga_maven_password
                }
            }
        }
        else {
            System.out.println("Remote publish disabled.")
            maven {
                url "$buildDir/repo"
            }
        }
    }
}
