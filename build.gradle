group 'dev.gigaherz.util.gddl'
version '2.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceCompatibility = targetCompatibility = 11
compileJava {
    sourceCompatibility = targetCompatibility = 11
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:20.1.0'

    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile "com.google.guava:guava:29.0-jre"
}

sourceSets {
    demo {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    demoCompile.extendsFrom compile
    demoRuntime.extendsFrom runtime
}

tasks.build.dependsOn compileDemoJava

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId 'gddl'

            artifact sourcesJar
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "GDDL",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "2.0", // We are version 1 of ourselves
                "Implementation-Title": "GDDL",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}